plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'org.openapi.generator' version '7.0.1'
}

group = 'com.task.management'
version = '1.0-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux:3.5.6'
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc:3.5.6'
    implementation 'io.projectreactor.netty:reactor-netty-http:1.1.7'
    implementation 'io.r2dbc:r2dbc-postgresql:0.8.13.RELEASE'
    implementation 'org.flywaydb:flyway-core:11.13.2'
    runtimeOnly 'org.flywaydb:flyway-database-postgresql:11.13.2'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.7'
    implementation 'org.springframework.boot:spring-boot-starter-validation:3.5.6'
    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'
    implementation 'org.postgresql:postgresql:42.7.3'
    implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.7.0'
}

test {
    useJUnitPlatform()
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$rootDir/src/main/resources/contract.yml"
    outputDir = "$buildDir/generated"
    apiPackage = "com.task.management.api"
    modelPackage = "com.task.management.dto"
    configOptions = [
            additionalModelTypeAnnotations: "@lombok.Builder @lombok.Data @lombok.AllArgsConstructor",
            useBeanValidation: "true",
            reactive: "true",
            returnResponse: "true",
            useSpringController: "false",
            useSpringBoot3: "true",
            dateLibrary: "java8",
            interfaceOnly: "true",
            useTags: "true"
    ]
    typeMappings = [
            OffsetDateTime: "java.time.ZonedDateTime",
            ErrorMessage: "com.task.management.exception.ErrorMessage"
    ]
    importMappings = [
            ErrorMessage: "com.task.management.exception.ErrorMessage"
    ]
    schemaMappings = [
            ErrorMessage: "com.task.management.exception.ErrorMessage"
    ]

}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/src/main/java"
        }
    }
}

compileJava.dependsOn tasks.openApiGenerate