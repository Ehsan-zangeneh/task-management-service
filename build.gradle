plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'org.openapi.generator' version '7.0.1'
    id 'jacoco'
}

group = 'com.task.management'
version = '1.0-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux:3.5.6'
    implementation 'io.projectreactor.netty:reactor-netty:1.2.10'
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc:3.5.6'
    implementation 'io.r2dbc:r2dbc-postgresql:0.8.13.RELEASE'
    implementation 'org.postgresql:postgresql:42.7.3'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.7'
    implementation 'org.springframework.boot:spring-boot-starter-validation:3.5.6'
    implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.7.0'
    implementation 'org.flywaydb:flyway-core:11.13.2'
    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:3.5.6'
    testImplementation 'io.projectreactor:reactor-test:3.8.0-M7'
    testImplementation 'org.testcontainers:junit-jupiter:1.21.3'
    testImplementation 'org.testcontainers:r2dbc:1.21.3'
    testImplementation 'org.testcontainers:postgresql:1.21.3'
    runtimeOnly 'org.flywaydb:flyway-database-postgresql:11.13.2'
}

test {
    useJUnitPlatform()
}

jacoco {
    toolVersion = "0.8.10"
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}
jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.49 // should increase to 0.70
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$rootDir/src/main/resources/contract.yml"
    outputDir = "$buildDir/generated"
    apiPackage = "com.task.management.api"
    modelPackage = "com.task.management.dto"
    configOptions = [
            additionalModelTypeAnnotations: "@lombok.Builder @lombok.Data @lombok.AllArgsConstructor",
            useBeanValidation: "true",
            reactive: "true",
            returnResponse: "true",
            useSpringController: "false",
            useSpringBoot3: "true",
            dateLibrary: "java8",
            interfaceOnly: "true",
            useTags: "true"
    ]
    typeMappings = [
            OffsetDateTime: "java.time.ZonedDateTime"
    ]
    schemaMappings = [
            ErrorMessage: "com.task.management.exception.ErrorMessage"
    ]

}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/src/main/java"
        }
    }
}

compileJava.dependsOn tasks.openApiGenerate